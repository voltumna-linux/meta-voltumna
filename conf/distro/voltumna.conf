DISTRO = "voltumna"
DISTRO_NAME ??= "Voltumna"
DISTRO_CODENAME = "XXXXXXXXXXXXXXXXXXX"
DISTRO_VERSION = "master${IMAGE_VERSION}"
SDK_VENDOR = "-voltumnasdk"
SDK_VERSION = "${DISTRO_VERSION}"
SDK_VERSION[vardepvalue] = "${SDK_VERSION}"

MAINTAINER = "Alessio Igor Bogani <alessio.bogani@elettra.eu>"

TARGET_VENDOR = "-voltumna"

DISTRO_FEATURES = "acl xattr largefile keyboard ldconfig pci pam systemd \
	usbgadget usbhost wifi usrmerge polkit \
	api-documentation"

# Remove few distro features because we don't support bluetooth,
# audio and traditional init system
DISTRO_FEATURES_BACKFILL_CONSIDERED = " \
	pulseaudio sysvinit bluez5"

MACHINE_FEATURES += " qemu-usermode"

# Use reasonable kernel, image and sdk filenames
KERNEL_ARTIFACT_NAME = "${MACHINE}-${PKGE}-${PKGV}-${PKGR}"
KERNEL_IMAGEDEST = "usr/boot"

SDK_NAME_PREFIX = "${IMAGE_BASENAME}"
SDK_NAME = "${SDK_NAME_PREFIX}-${SDKMACHINE}-${MACHINE}"
SDKPATHINSTALL = "/opt/${SDK_NAME_PREFIX}/${SDK_VERSION}"
TOOLCHAIN_OUTPUTNAME = "${SDK_NAME}-${SDK_VERSION}"
SDK_PS1 = "${DISTRO_NAME} ${SDK_VERSION}"

IMAGE_NAME_SUFFIX = ""
IMAGE_VERSION_SUFFIX = ""
IMAGE_NAME = "${IMAGE_BASENAME}-${MACHINE}-${DISTRO_VERSION}"

# Use systemd
INIT_MANAGER = "systemd"
VIRTUAL-RUNTIME_init_manager = "systemd"
VIRTUAL-RUNTIME_initscripts = ""

# Avoid busybox
VIRTUAL-RUNTIME_base-utils = ""
VIRTUAL-RUNTIME_base-utils-hwclock = ""
VIRTUAL-RUNTIME_base-utils-syslog = ""
VIRTUAL-RUNTIME_login_manager = ""

# Disable creation of the tarball of modules
MODULE_TARBALL_DEPLOY = "0"

FILESYSTEM_PERMS_TABLES = "files/fs-perms-voltumna.txt"

# Avoid to download and use new packages inadvertently
BB_NO_NETWORK ??= "1"

# We trying to use the same kernel and booloader release among all our boards
OLDEST_KERNEL = "6.12"
LINUXLIBCVERSION = "6.12"
PREFERRED_VERSION_virtual/kernel ?= "6.12%"
PREFERRED_VERSION_linux-libc-headers ?= "6.12%"
PREFERRED_VERSION_linux-intel = "${PREFERRED_VERSION_virtual/kernel}"
PREFERRED_VERSION_linux-intel-rt = "${PREFERRED_VERSION_virtual/kernel}"
PREFERRED_VERSION_linux-yocto = "${PREFERRED_VERSION_virtual/kernel}"
PREFERRED_VERSION_linux-yocto-rt = "${PREFERRED_VERSION_virtual/kernel}"
#PREFERRED_VERSION_linux-bb.org = "${PREFERRED_VERSION_virtual/kernel}"
#PREFERRED_VERSION_linux-socfpga-lts = "${PREFERRED_VERSION_virtual/kernel}"
PREFERRED_VERSION_linux-yocto-epyc = "${PREFERRED_VERSION_virtual/kernel}"

#PREFERRED_VERSION_libbpf = "1.3.0"

PREFERRED_VERSION_nativesdk-libgfortran = "${GCCVERSION}"

PREFERRED_VERSION_tango-idl = "6.%"
PREFERRED_VERSION_nativesdk-tango-idl = "6.%"
PREFERRED_VERSION_cpptango = "10.1.%"
PREFERRED_VERSION_nativesdk-cpptango = "10.1.%"
PREFERRED_VERSION_python3-numpy = "2.2%"
PREFERRED_VERSION_python3-numpy-native = "2.2%"
PREFERRED_VERSION_nativesdk-python3-numpy = "2.2%"
PREFERRED_VERSION_python3-pytango = "10.1.%"
PREFERRED_VERSION_nativesdk-python3-pytango = "10.1.%"
PREFERRED_VERSION_nativesdk-pogo = "9.7%"

PREFERRED_VERSION_python3-setuptools-scm = "8.3.1"

# Use specific GCC version
GCCVERSION = "15.%"

# Use an initramfs
INITRAMFS_IMAGE_BUNDLE = "1"
INITRAMFS_IMAGE = "voltumna-initramfs"

# Set few defaults
ROOT_HOME = "/root"
LOCALE_UTF8_ONLY = "1"

# For the bios recipe
HOSTTOOLS += "pdftotext"

# Set http server's root
WWWDIR = "${datadir}/www/localhost/html"

# Specific to TI
TI_COMMON_DEPLOY = "${TOPDIR}/tmp/deploy"

PACKAGE_CLASSES ?= "package_rpm"

SANITY_TESTED_DISTROS = " \
	ubuntu-24.04\n"

# QA check settings - a little stricter than the OE-Core defaults
WARN_TO_ERROR_QA = "already-stripped compile-host-path install-host-path \
                    installed-vs-shipped ldflags pn-overrides rpaths     \
                    staticdev unknown-configure-option useless-rpaths"
WARN_QA:remove = "${WARN_TO_ERROR_QA}"
ERROR_QA:append = " ${WARN_TO_ERROR_QA}"
WARN_QA:append = "shebang-size"
ERROR_QA:remove = "shebang-size"

# Avoid use of static libs
require conf/distro/include/no-static-libs.inc
# Leverage Yocto uninative tarball
require conf/distro/include/yocto-uninative.inc
INHERIT += "uninative"

BB_SIGNATURE_HANDLER ?= "OEEquivHash"
BB_HASHSERVE ??= "auto"
