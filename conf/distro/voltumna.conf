DISTRO = "voltumna"
DISTRO_NAME = "Voltumna"
DISTRO_CODENAME = "Highway Tune"
DISTRO_VERSION = "0.10beta1${IMAGE_VERSION}"
SDK_VENDOR = "-voltumnasdk"

MAINTAINER = "Alessio Igor Bogani <alessio.bogani@elettra.eu>"

TARGET_VENDOR = "-voltumna"

# The distro feature api-documentation produces a build error on powerpcspe:
# /lib/ld.so.1: No such file or directory
DISTRO_FEATURES = "acl xattr largefile keyboard ldconfig pci pam systemd \
	stateless-rootfs usbgadget usbhost wifi usrmerge opengl polkit"

# Remove few distro features because we don't support bluetooth
# and gobject-introspection produces a build error on powerpcspe:
# /lib/ld.so.1: No such file or directory
DISTRO_FEATURES_BACKFILL_CONSIDERED = " \
	pulseaudio sysvinit bluez5 \
	gobject-introspection-data"

# Use reasonable kernel, image and sdk filenames
KERNEL_ARTIFACT_NAME = "${MACHINE}-${PKGE}-${PKGV}-${PKGR}"
KERNEL_IMAGEDEST = "usr/boot"

IMAGE_NAME_SUFFIX = ""
IMAGE_VERSION_SUFFIX = ""
IMAGE_NAME = "${IMAGE_BASENAME}-${MACHINE}-${DISTRO_VERSION}"

SDK_VERSION = "${DISTRO_VERSION}"
SDK_NAME = "${SDK_NAME_PREFIX}-${SDKMACHINE}-${TUNE_PKGARCH}"
SDKPATH = "/opt/${SDK_NAME_PREFIX}/${SDK_VERSION}"
TOOLCHAIN_OUTPUTNAME = "${SDK_NAME}-${SDK_VERSION}"

# Use systemd
VIRTUAL-RUNTIME_init_manager = "systemd"
VIRTUAL-RUNTIME_initscripts = ""

# Avoid busybox
VIRTUAL-RUNTIME_base-utils = ""
VIRTUAL-RUNTIME_base-utils-hwclock = ""
VIRTUAL-RUNTIME_base-utils-syslog = ""
VIRTUAL-RUNTIME_login_manager = ""

# Disable creation of the tarball of modules
MODULE_TARBALL_DEPLOY = "0"

# Relay on systemd for volatile implementation
VOLATILE_LOG_DIR = "no"
FILESYSTEM_PERMS_TABLES = "files/fs-perms-voltumna.txt"

# Avoid to download and use new packages inadvertently
BB_NO_NETWORK ??= "1"

# We trying to use the same kernel and booloader release among all our boards
OLDEST_KERNEL = "5.4"
LINUXLIBCVERSION="5.4"
PREFERRED_VERSION_virtual/kernel = "5.4%"
PREFERRED_VERSION_u-boot-ti-staging = "2020.01+gitAUTOINC+eea7380073"
PREFERRED_VERSION_u-boot-ti-staging-mmc1 = "${PREFERRED_VERSION_u-boot-ti-staging}"
# PREFERRED_VERSION_linux-ti-staging = "4.19%"
# PREFERRED_VERSION_u-boot-socfpga = "v2019.04+gitAUTOINC+bc38a09daa"

# Use specific GCC version (due MVME2500)
GCCVERSION = "9.%"

# Use an initramfs
INITRAMFS_IMAGE_BUNDLE = "1"
INITRAMFS_IMAGE = "voltumna-initramfs"

# Set few defaults
ROOT_HOME = "/root"
LOCALE_UTF8_ONLY = "1"

# Set http server's root
WWWDIR = "${datadir}/www/localhost"

# We support only one libc (glibc)
TCLIBCAPPEND = ""

PREMIRRORS ??= "\
bzr://.*/.*   http://downloads.yoctoproject.org/mirror/sources/ \n \
cvs://.*/.*   http://downloads.yoctoproject.org/mirror/sources/ \n \
git://.*/.*   http://downloads.yoctoproject.org/mirror/sources/ \n \
gitsm://.*/.* http://downloads.yoctoproject.org/mirror/sources/ \n \
hg://.*/.*    http://downloads.yoctoproject.org/mirror/sources/ \n \
osc://.*/.*   http://downloads.yoctoproject.org/mirror/sources/ \n \
p4://.*/.*    http://downloads.yoctoproject.org/mirror/sources/ \n \
svn://.*/.*   http://downloads.yoctoproject.org/mirror/sources/ \n"

SANITY_TESTED_DISTROS = " \
	ubuntu-18.04 \n"

# Notify users when the format of TMPDIR changes in an incompatible way
OELAYOUT_ABI = "12"

# QA check settings - a little stricter than the OE-Core defaults
WARN_TO_ERROR_QA = "already-stripped compile-host-path install-host-path \
                    installed-vs-shipped ldflags pn-overrides rpaths     \
                    staticdev unknown-configure-option useless-rpaths"
WARN_QA_remove = "${WARN_TO_ERROR_QA}"
ERROR_QA_append = " ${WARN_TO_ERROR_QA}"

# Avoid use of static libs
require conf/distro/include/no-static-libs.inc
# Leverage Yocto uninative tarball
require conf/distro/include/yocto-uninative.inc
INHERIT += "uninative"

# Avoid to build qemu for sdk
SDK_DEPENDS_remove = "nativesdk-qemuwrapper-cross"
RDEPENDS_nativesdk-packagegroup-sdk-host_remove = "nativesdk-qemu nativesdk-qemu-helper"
RDEPENDS_nativesdk-packagegroup-sdk-host_remove_mingw32 = "nativesdk-qemu nativesdk-qemu-helper"
