DISTRO = "voltumna"
DISTRO_NAME ??= "Voltumna"
DISTRO_CODENAME = "Highway Tune"
DISTRO_VERSION = "1.12${IMAGE_VERSION}"
SDK_VENDOR = "-voltumnasdk"
SDK_VERSION = "${DISTRO_VERSION}"
SDK_VERSION[vardepvalue] = "${SDK_VERSION}"

MAINTAINER = "Alessio Igor Bogani <alessio.bogani@elettra.eu>"

TARGET_VENDOR = "-voltumna"

DISTRO_FEATURES = "acl xattr largefile keyboard ldconfig pci pam systemd \
	stateless-rootfs usbgadget usbhost wifi usrmerge polkit \
	api-documentation"

# Remove few distro features because we don't support bluetooth, audio,
# traditional init system and gobject-introspection. Moreover the latter produces
# a build error on powerpcspe: /lib/ld.so.1: No such file or directory
DISTRO_FEATURES_BACKFILL_CONSIDERED = " \
	pulseaudio sysvinit bluez5 \
	gobject-introspection-data"

MACHINE_FEATURES += " qemu-usermode"

# Use reasonable kernel, image and sdk filenames
KERNEL_ARTIFACT_NAME = "${MACHINE}-${PKGE}-${PKGV}-${PKGR}"
KERNEL_IMAGEDEST = "usr/boot"

SDK_NAME_PREFIX = "${IMAGE_BASENAME}"
SDK_NAME = "${SDK_NAME_PREFIX}-${SDKMACHINE}-${MACHINE}"
SDKPATHINSTALL = "/opt/${SDK_NAME_PREFIX}/${SDK_VERSION}"
TOOLCHAIN_OUTPUTNAME = "${SDK_NAME}-${SDK_VERSION}"
SDK_PS1 = "${DISTRO_NAME} ${SDK_VERSION}"

IMAGE_NAME_SUFFIX = ""
IMAGE_VERSION_SUFFIX = ""
IMAGE_NAME = "${IMAGE_BASENAME}-${MACHINE}-${DISTRO_VERSION}"

# Use systemd
INIT_MANAGER = "systemd"
VIRTUAL-RUNTIME_init_manager = "systemd"
VIRTUAL-RUNTIME_initscripts = ""

# Avoid busybox
VIRTUAL-RUNTIME_base-utils = ""
VIRTUAL-RUNTIME_base-utils-hwclock = ""
VIRTUAL-RUNTIME_base-utils-syslog = ""
VIRTUAL-RUNTIME_login_manager = ""

# Disable creation of the tarball of modules
MODULE_TARBALL_DEPLOY = "0"

# Relay on systemd for volatile implementation
VOLATILE_LOG_DIR = "no"
FILESYSTEM_PERMS_TABLES = "files/fs-perms-voltumna.txt"

# Avoid to download and use new packages inadvertently
BB_NO_NETWORK ??= "1"

# We trying to use the same kernel and booloader release among all our boards
OLDEST_KERNEL = "5.4"
LINUXLIBCVERSION = "5.4"
PREFERRED_VERSION_virtual/kernel ?= "5.4%"
PREFERRED_VERSION_linux-libc-headers ?= "5.4%"
PREFERRED_VERSION_linux-intel = "${PREFERRED_VERSION_virtual/kernel}"
PREFERRED_VERSION_linux-intel-rt = "${PREFERRED_VERSION_virtual/kernel}"
PREFERRED_VERSION_linux-ti-staging = "${PREFERRED_VERSION_virtual/kernel}"
PREFERRED_VERSION_linux-ti-staging-rt = "${PREFERRED_VERSION_virtual/kernel}"
PREFERRED_PROVIDER_virtual/kernel_sockit = "linux-altera-lts"
PREFERRED_VERSION_linux-altera-lts = "${PREFERRED_VERSION_virtual/kernel}"
PREFERRED_VERSION_u-boot-ti-staging = "2020.01+gitAUTOINC+2781231a33"
PREFERRED_VERSION_u-boot-ti-staging-mmc1 = "${PREFERRED_VERSION_u-boot-ti-staging}"
PREFERRED_VERSION_u-boot-socfpga = "1:v2021.04+gitAUTOINC+fcb08e4ed9"

PREFERRED_VERSION_nativesdk-libgfortran = "${GCCVERSION}"

PREFERRED_VERSION_tango-idl = "5.1%"
PREFERRED_VERSION_nativesdk-tango-idl = "5.1%"
PREFERRED_VERSION_cpptango = "9.3%"
PREFERRED_VERSION_nativesdk-cpptango = "9.3%"
PREFERRED_VERSION_python3-pytango = "9.3%"
PREFERRED_VERSION_nativesdk-python3-pytango = "9.3%"
PREFERRED_VERSION_nativesdk-pogo = "9.7%"

# Use specific GCC version
GCCVERSION = "9.%"

# Use an initramfs
INITRAMFS_IMAGE_BUNDLE = "1"
INITRAMFS_IMAGE = "voltumna-initramfs"

# Set few defaults
ROOT_HOME = "/root"
LOCALE_UTF8_ONLY = "1"

# For the bios recipe
HOSTTOOLS += "pdftotext"

# Set http server's root
WWWDIR = "${datadir}/www/localhost/html"

# Specific to TI
TI_COMMON_DEPLOY = "${TOPDIR}/tmp/deploy"

# We support only one libc (glibc)
TCLIBCAPPEND = ""

PACKAGE_CLASSES ?= "package_rpm"

SANITY_TESTED_DISTROS = " \
	ubuntu-24.04\n"

# QA check settings - a little stricter than the OE-Core defaults
WARN_TO_ERROR_QA = "already-stripped compile-host-path install-host-path \
                    installed-vs-shipped ldflags pn-overrides rpaths     \
                    staticdev unknown-configure-option useless-rpaths"
WARN_QA_remove = "${WARN_TO_ERROR_QA}"
ERROR_QA_append = " ${WARN_TO_ERROR_QA}"
WARN_QA_append = "shebang-size"
ERROR_QA_remove = "shebang-size"

# Avoid use of static libs
require conf/distro/include/no-static-libs.inc
# Leverage Yocto uninative tarball
require conf/distro/include/yocto-uninative.inc
INHERIT += "uninative"

BB_SIGNATURE_HANDLER ?= "OEEquivHash"
BB_HASHSERVE ??= "auto"
