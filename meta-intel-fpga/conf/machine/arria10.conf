#@TYPE: Machine
#@NAME: Intel SoCFPGA Arria10
#@DESCRIPTION: Machine configuration for the Arria 10 SoCFPGA

require conf/machine/include/socfpga.inc

SPL_BINARY = "spl/u-boot-splx4.sfp"
UBOOT_BINARY = "u-boot.img"

UBOOT_CONFIG ??= "arria10-socdk"
UBOOT_CONFIG[arria10-socdk] = "socfpga_arria10_defconfig"
UBOOT_CONFIG[arria10-socdk-nand] = "socfpga_arria10_nand_defconfig"
UBOOT_CONFIG[arria10-socdk-qspi] = "socfpga_arria10_qspi_defconfig"

KMACHINE = "arria10"

# Default kernel devicetrees
KERNEL_DEVICETREE ?= "\
			socfpga_arria10_socdk_sdmmc.dtb \
			socfpga_arria10_socdk_qspi.dtb \
			socfpga_arria10_socdk_nand.dtb \
			"

EXTRA_IMAGECMD:jffs2 ?= "-n -q \
	${@bb.utils.contains("IMAGE_TYPE", "nand", "-e 0x20000 -s 0x200", "", d)} \
	${@bb.utils.contains("IMAGE_TYPE", "qspi", "-e 0x10000", "", d)} \
	"

SERIAL_CONSOLES ?= "115200;ttyS0"

UBOOT_EXTLINUX ?= "1"
UBOOT_EXTLINUX_LABELS ?= "default"
UBOOT_EXTLINUX_DEFAULT_LABEL ?= "Arria10 SOCDK SDMMC"

UBOOT_EXTLINUX_MENU_DESCRIPTION:default ?= "Arria10 SOCDK SDMMC"
UBOOT_EXTLINUX_KERNEL_IMAGE:default ?= "../${KERNEL_IMAGETYPE}"
UBOOT_EXTLINUX_FDTDIR:default ?= ".."
UBOOT_EXTLINUX_CONSOLE ?= "console=ttyS0,115200n8"
UBOOT_EXTLINUX_KERNEL_ARGS:default ?= "rootwait rw earlyprintk memmap=16M$0x3F000000"
UBOOT_EXTLINUX_ROOT:default ?= "root=/dev/mmcblk0p2"

IMAGE_BOOT_FILES ?= " \
	${KERNEL_DEVICETREE} \
	${KERNEL_IMAGETYPE} \
	${UBOOT_BINARY} \
	extlinux.conf;extlinux/extlinux.conf \
        "

# Build both Image and zImage for Arria10
KERNEL_ALT_IMAGETYPE = "Image"

WKS_FILE ?= "sdimage-arria10.wks"
IMAGE_FSTYPES:append = " wic"
